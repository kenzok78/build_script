From bba01f8fcecc39dd89274d977508dd50ba620f65 Mon Sep 17 00:00:00 2001
From: Mubashir Adnan Qureshi <mubashirq@google.com>
Date: Thu, 21 Jul 2022 19:03:30 +0000
Subject: [PATCH 27/27] net-test: tcp: plb: Add PLB tests

Add tests to check the following operations of PLB

1. Forced rehashing
2. Idle rehashing when no packets in flight
3. PLB state reset upon RTO

Change-Id: If74c7fb0107c8f9a4f740dcb2e488c73dd6cc08d
---
 .../net/tcp/plb/bbr2-ecn-plb-idle-rehash.pkt  |  59 +++++++
 gtests/net/tcp/plb/bbr2-ecn-plb-rehash.pkt    | 146 ++++++++++++++++++
 .../tcp/plb/bbr2-ecn-plb-rto-suspend-off.pkt  |  71 +++++++++
 3 files changed, 276 insertions(+)
 create mode 100644 gtests/net/tcp/plb/bbr2-ecn-plb-idle-rehash.pkt
 create mode 100644 gtests/net/tcp/plb/bbr2-ecn-plb-rehash.pkt
 create mode 100644 gtests/net/tcp/plb/bbr2-ecn-plb-rto-suspend-off.pkt

diff --git a/gtests/net/tcp/plb/bbr2-ecn-plb-idle-rehash.pkt b/gtests/net/tcp/plb/bbr2-ecn-plb-idle-rehash.pkt
new file mode 100644
index 000000000000..706fbf72a66c
--- /dev/null
+++ b/gtests/net/tcp/plb/bbr2-ecn-plb-idle-rehash.pkt
@@ -0,0 +1,59 @@
+// Verify BBR v2 changes flowlabel in ipv6 header when there are
+// net.ipv4.tcp_plb_idle_rehash_rounds=3 consecutive rounds with the ECN mark rate >=
+// net.ipv4.tcp_plb_cong_thresh=64(25%) and there are no packets in flight.
+
+`../common/defaults.sh
+sysctl -q net.ipv4.tcp_ecn=1  # fully enabled
+sysctl -q net.ipv4.tcp_congestion_control=bbr2
+sysctl -q net.ipv4.tcp_plb_enabled=1
+sysctl -q net.ipv4.tcp_plb_cong_thresh=64
+sysctl -q net.ipv4.tcp_plb_idle_rehash_rounds=3
+sysctl -q net.ipv4.tcp_plb_rehash_rounds=12
+echo 1 > /sys/module/tcp_bbr2/parameters/ecn_enable
+echo 0 > /sys/module/tcp_bbr2/parameters/ecn_max_rtt_us
+`
+
+// Initialize connection
+    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
+   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
+   +0 bind(3, ..., ...) = 0
+   +0 listen(3, 1) = 0
+
+   +0 < SEW 0:0(0) ack 1 win 32792 <mss 1012,sackOK,TS val 200 ecr 0,nop,wscale 7>
+   +0 > (flowlabel 0x1) SE. 0:0(0) ack 1 <mss 1460,sackOK,TS val 100 ecr 200,nop,wscale 8>
+   +0 < . 1:1(0) ack 1 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 accept(3, ..., ...) = 4
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) P. 1:10001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < . 1:1(0) ack 10001 win 1000 <nop,nop,TS val 200 ecr 100>
+// no ECN mark, flowlabel won't change.
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) P. 10001:20001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 17001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 20001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 3/10 packets ECN-marked, >25%, 1 congested round
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) PW. 20001:30001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 27001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 30001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 3/10 packets ECN-marked, >25%, 2 consecutive congested rounds
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) PW. 30001:40001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 37001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 40001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 3/10 packets ECN-marked, >25%, 3 consecutive congested round
+
+   +0 write(4, ..., 1000) = 1000
+   +0 > (flowlabel 0x1) PW. 40001:41001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 41001 win 1000 <nop,nop,TS val 200 ecr 100>
+// This ACK started a new round. PLB observes that past three rounds were
+// congested and no packets are in flight so it changes the flowlabel.
+
+   +0 write(4, ..., 10000) = 10000
+// As expected, packets of idle write carry new flow label
+   +0 > (flowlabel 0x2) P. 41001:51001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 51001 win 1000 <nop,nop,TS val 200 ecr 100>
diff --git a/gtests/net/tcp/plb/bbr2-ecn-plb-rehash.pkt b/gtests/net/tcp/plb/bbr2-ecn-plb-rehash.pkt
new file mode 100644
index 000000000000..af500ef08768
--- /dev/null
+++ b/gtests/net/tcp/plb/bbr2-ecn-plb-rehash.pkt
@@ -0,0 +1,146 @@
+// Verify BBR v2 changes flowlabel in ipv6 header if and only if there are
+// net.ipv4.tcp_plb_rehash_rounds=3 consecutive rounds with the ECN mark rate >=
+// net.ipv4.tcp_plb_cong_thresh=64(25%).
+
+`../common/defaults.sh
+sysctl -q net.ipv4.tcp_ecn=1  # fully enabled
+sysctl -q net.ipv4.tcp_congestion_control=bbr2
+sysctl -q net.ipv4.tcp_plb_enabled=1
+sysctl -q net.ipv4.tcp_plb_cong_thresh=64
+sysctl -q net.ipv4.tcp_plb_idle_rehash_rounds=0
+sysctl -q net.ipv4.tcp_plb_rehash_rounds=3
+echo 1 > /sys/module/tcp_bbr2/parameters/ecn_enable
+echo 0 > /sys/module/tcp_bbr2/parameters/ecn_max_rtt_us
+`
+
+// Initialize connection
+    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
+   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
+   +0 bind(3, ..., ...) = 0
+   +0 listen(3, 1) = 0
+
+   +0 < SEW 0:0(0) ack 1 win 32792 <mss 1012,sackOK,TS val 200 ecr 0,nop,wscale 7>
+   +0 > (flowlabel 0x1) SE. 0:0(0) ack 1 <mss 1460,sackOK,TS val 100 ecr 200,nop,wscale 8>
+   +0 < . 1:1(0) ack 1 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 accept(3, ..., ...) = 4
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) P. 1:10001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < . 1:1(0) ack 10001 win 1000 <nop,nop,TS val 200 ecr 100>
+// no ECN mark, flowlabel won't change.
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) P. 10001:20001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 19001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 20001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 1/10 packets ECN-marked. Not enough.
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) PW. 20001:30001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 28001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 30001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 2/10 packets ECN-marked. Not enough.
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) PW. 30001:40001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 37001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 40001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 3/10 packets ECN-marked, >25%, but only 1 round.
+
+   +0 write(4, ..., 1000) = 1000
+   +0 > (flowlabel 0x1) PW. 40001:41001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 41001 win 1000 <nop,nop,TS val 200 ecr 100>
+// no ECN mark, flowlabel won't change.
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) P. 41001:51001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 48001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 51001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 3/10 packets ECN-marked, >25%, but only 1 round.
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) PW. 51001:61001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 58001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 61001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 3/10 packets ECN-marked, >25%, 2 consecutive rounds.
+
+   +0 write(4, ..., 1000) = 1000
+   +0 > (flowlabel 0x1) PW. 61001:62001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 62001 win 1000 <nop,nop,TS val 200 ecr 100>
+// no ECN mark, flowlabel won't change.
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) P. 62001:72001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 69001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 72001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 3/10 packets ECN-marked, >25%, but only 1 round.
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) PW. 72001:82001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 79001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 82001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 3/10 packets ECN-marked, >25%, 2 consecutive rounds.
+
+   +0 write(4, ..., 1000) = 1000
+   +0 > (flowlabel 0x1) PW. 82001:83001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 83001 win 1000 <nop,nop,TS val 200 ecr 100>
+// no ECN mark, flowlabel won't change.
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) P. 83001:93001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 90001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 93001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 3/10 packets ECN-marked, >25%, but only 1 round.
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) PW. 93001:103001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 100001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 103001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 3/10 packets ECN-marked, >25%, 2 consecutive rounds.
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x1) PW. 103001:113001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 110001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 113001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 3/10 packets ECN-marked, >25%, 3 consecutive rounds.
+
+   +0 write(4, ..., 10000) = 10000
+// Since a new round haven't started, flowlabel is not updated.
+   +0 > (flowlabel 0x1) PW. 113001:123001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 120001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 123001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 3/10 packets ECN-marked, >25%, 1 consecutive rounds.
+
+   +0 write(4, ..., 10000) = 10000
+// Verify the new flowlabel
+   +0 > (flowlabel 0x2) PW. 123001:133001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 130001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 133001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 3/10 packets ECN-marked, >25%, 2 consecutive rounds.
+
+   +0 write(4, ..., 10000) = 10000
+   +0 > (flowlabel 0x2) PW. 133001:143001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 140001 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 < E. 1:1(0) ack 143001 win 1000 <nop,nop,TS val 200 ecr 100>
+// 3/10 packets ECN-marked, >25%, 3 consecutive rounds. flowlabel will change in a new round.
+
+   +0 write(4, ..., 1000) = 1000
+// Since a new round haven't started, flowlabel is not updated.
+   +0 > (flowlabel 0x2) PW. 143001:144001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 144001 win 1000 <nop,nop,TS val 200 ecr 100>
+// A new round starts after the ack above, new flowlabel assigned from here.
+
+// Verify the new flowlabel
+   +0 write(4, ..., 1000) = 1000
+   +0 > (flowlabel 0x3) P. 144001:145001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 145001 win 1000 <nop,nop,TS val 200 ecr 100>
+
+// Verify the new flowlabel sticks and does't change again
+   +0 write(4, ..., 1000) = 1000
+   +0 > (flowlabel 0x3) P. 145001:146001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 146001 win 1000 <nop,nop,TS val 200 ecr 100>
+
+// Verify the new flowlabel sticks and does't change again
+   +0 write(4, ..., 1000) = 1000
+   +0 > (flowlabel 0x3) P. 146001:147001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 147001 win 1000 <nop,nop,TS val 200 ecr 100>
diff --git a/gtests/net/tcp/plb/bbr2-ecn-plb-rto-suspend-off.pkt b/gtests/net/tcp/plb/bbr2-ecn-plb-rto-suspend-off.pkt
new file mode 100644
index 000000000000..d46e759e57e5
--- /dev/null
+++ b/gtests/net/tcp/plb/bbr2-ecn-plb-rto-suspend-off.pkt
@@ -0,0 +1,71 @@
+// Verify BBR v2 changes flowlabel in ipv6 header when there are
+// net.ipv4.tcp_plb_idle_rehash_rounds=3 consecutive rounds with
+// the ECN mark rate >= net.ipv4.tcp_plb_cong_thresh=64(25%) after
+// an RTO resets PLB state. This test confirms that PLB state is reset
+// after an RTO. TLP is disabled for this test.
+
+`../common/defaults.sh
+sysctl -q net.ipv4.tcp_ecn=1  # fully enabled
+sysctl -q net.ipv4.tcp_congestion_control=bbr2
+sysctl -q net.ipv4.tcp_plb_enabled=1
+sysctl -q net.ipv4.tcp_plb_cong_thresh=64
+sysctl -q net.ipv4.tcp_plb_idle_rehash_rounds=0
+sysctl -q net.ipv4.tcp_plb_rehash_rounds=3
+sysctl -q net.ipv4.tcp_plb_suspend_rto_sec=0
+sysctl -q net.ipv4.tcp_early_retrans=0
+sysctl -q net.ipv4.tcp_recovery=0
+echo 1 > /sys/module/tcp_bbr2/parameters/ecn_enable
+echo 0 > /sys/module/tcp_bbr2/parameters/ecn_max_rtt_us
+`
+
+// Initialize connection
+    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
+   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
+   +0 bind(3, ..., ...) = 0
+   +0 listen(3, 1) = 0
+
+   +0 < SEW 0:0(0) ack 1 win 32792 <mss 1012,sackOK,TS val 200 ecr 0,nop,wscale 7>
+   +0 > (flowlabel 0x1) SE. 0:0(0) ack 1 <mss 1460,sackOK,TS val 100 ecr 200,nop,wscale 8>
+   +0 < . 1:1(0) ack 1 win 1000 <nop,nop,TS val 200 ecr 100>
+   +0 accept(3, ..., ...) = 4
+
+   +0 write(4, ..., 1000) = 1000
+   +0 > (flowlabel 0x1) P. 1:1001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < E. 1:1(0) ack 1001 win 1000 <nop,nop,TS val 200 ecr 100>
+// ECN mark, 1 congested round
+
+   +0 write(4, ..., 1000) = 1000
+   +0 > (flowlabel 0x1) PW. 1001:2001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < E.  1:1(0) ack 2001 win 1000 <nop,nop,TS val 200 ecr 100>
+// ECN mark, 2 consecutive congested rounds.
+
+   +0 write(4, ..., 1000) = 1000
+   +0 > (flowlabel 0x1) PW. 2001:3001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+// Flowlabel should be changed after kernel retransmit
++.2~+.22  > (flowlabel 0x2) P. 2001:3001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < E. 1:1(0) ack 3001 win 1000 <nop,nop,TS val 200 ecr 100>
+// PLB state should reset, 1 congested round
+
+   +0 write(4, ..., 1000) = 1000
+   +0 > (flowlabel 0x2) PW. 3001:4001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < E.  1:1(0) ack 4001 win 1000 <nop,nop,TS val 200 ecr 100>
+// ECN mark, 2 consecutive congested rounds.
+
+   +0 write(4, ..., 1000) = 1000
+   +0 > (flowlabel 0x2) PW. 4001:5001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < E.  1:1(0) ack 5001 win 1000 <nop,nop,TS val 200 ecr 100>
+// ECN mark, 3 consecutive congested rounds. Flowlabel should change in next round.
+
+   +0 write(4, ..., 1000) = 1000
+   +0 > (flowlabel 0x3) PW. 5001:6001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 6001 win 1000 <nop,nop,TS val 200 ecr 100>
+
+   +0 write(4, ..., 1000) = 1000
+// Verify new flow label
+   +0 > (flowlabel 0x3) P. 6001:7001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 7001 win 1000 <nop,nop,TS val 200 ecr 100>
+
+   +0 write(4, ..., 1000) = 1000
+// Verify new flow label sticks
+   +0 > (flowlabel 0x3) P. 7001:8001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
+   +0 < .  1:1(0) ack 8001 win 1000 <nop,nop,TS val 200 ecr 100>
-- 
2.34.1

